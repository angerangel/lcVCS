on mouseUp
   set the opaque of me to false
   if the environment = "mobile" then
      answer "Save to library or email" with "Library" or "Email" or "Cancel"
      switch it
         case "Cancel"
            exit mouseUp
            break
         case "Library"
            export snapshot from group "drawing" to tImage as JPEG
            mobileExportImageToAlbum tImage
            break
         case "Email"
            export snapshot from group "drawing" to file specialFolderPath("temporary")&"/doodle.jpg" as JPEG
            put specialFolderPath("temporary")&"/doodle.jpg"  into tAttachment["file"]
            put "image/jpeg" into tAttachment["type"]
            put "Doodle" into tAttachment["name"]
            mobileComposeMail "Shake Your Doodle",,,, "Created with Shake Your Doodle", tAttachment
            break
      end switch
   else
      ask file "Save as" with type "JPEG Image|jpg"
      if it ="" then exit mouseUp
      put it into tFile
      set the itemDel to "."
      if item -1 of tFile <> "jpg" then
         if char -1 of tFile <> "." then
            put ".jpg" after tFile
         else
            put "jpg" after tFile
         end if
      end if
      export snapshot from group "drawing" to file tFile as JPEG
   end if
end mouseUp

on mouseDown
   set the opaque of me to true
end mouseDown

on mouseRelease
   set the opaque of me to false
end mouseRelease
