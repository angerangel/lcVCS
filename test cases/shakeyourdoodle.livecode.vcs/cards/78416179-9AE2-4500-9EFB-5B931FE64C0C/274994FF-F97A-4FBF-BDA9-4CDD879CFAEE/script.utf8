local sMouseDown

on mouseDown
   lock screen
   set the style of the templateGraphic to "polygon"
   if the environment = "mobile" then
      put 8*mobilePixelDensity() into tSize
   else
      put 8 into tSize
   end if
   set the lineSize of the templateGraphic to tSize
   set the points of the templateGraphic to the mouseLoc 
   set the layerMode of the templateGraphic to "dynamic"
   create graphic ("line "&the seconds) in me
   put true into sMouseDown
end mouseDown

on mouseMove x,y
   lock screen
   if sMouseDown and x,y is within the rect of grc "background" of me then
      set the points of the last grc of me to the points of the last grc & cr & x,y
   end if
end mouseMove

on mouseUp
   put false into sMouseDown
end mouseUp

on mouseRelease
   put false into sMouseDown
end mouseRelease

on randomLines
   lock screen
   repeat with X =1 to the number of graphics of me
      if word 1 of the short name of graphic X of me = "line" then
         set the foreColor of graphic X of me to random(256)-1,random(256)-1,random(256)-1
         if the environment = "mobile" then
            put (5+random(10))*mobilePixelDensity() into tSize
         else
            put 5+random(10) into tSize
         end if
         set the lineSize of graphic X of me to tSize
      end if
   end repeat
end randomLines
