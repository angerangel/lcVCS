
local sMessage,sMoveDirection,sTopCounter,sSmashedMole,sLaugingMole

on startGame
   send gameLoop to me in 20 milliseconds
   put the result into sMessage
end startGame

on gameLoop
   repeat with X=1 to 3
      moveMole the long name of button ("mole"&X)
   end repeat
   send gameLoop to me in 20 milliseconds
   put the result into sMessage
end gameLoop

on moveMole pMoleButton
   -- when the mole is all the way down the hole
   -- the top of the mole button is 60 pixels below the top of
   -- the grass lower image
   if the top of pMoleButton >= the top of image "grass lower"+60 then
      -- mole is in the hole
      if random(50) = 1 then
         set the icon of pMoleButton to "mole"
         put "up" into sMoveDirection[pMoleButton]
         set the top of pMoleButton to the top of image "grass lower"+55
         put false into sSmashedMole[pMoleButton]
      end if
   else if the bottom of pMoleButton <= the top of image "grass lower"+70 then
      -- mole is out of the hole
      if sTopCounter[pMoleButton] mod 3 = 0 then
         if sSmashedMole[pMoleButton] then
            if the icon of pMoleButton = the short id of image "mole_thump1" then
               set the icon of pMoleButton to "mole_thump2"
            else if the icon of pMoleButton = the short id of image  "mole_thump2" then
               set the icon of pMoleButton to "mole_thump3"
            else if the icon of pMoleButton = the short id of image "mole_thump3" then
               set the icon of pMoleButton to "mole_thump4"
            else if the icon of pMoleButton <> the short id of image "mole_thump4" then
               set the icon of pMoleButton to "mole_thump1"
            end if
         else
            if the icon of pMoleButton = the short id of image "mole" then
               set the icon of pMoleButton to "mole_laugh1"
            else if the icon of pMoleButton = the short id of image "mole_laugh1" then
               set the icon of pMoleButton to "mole_laugh2"
            else if the icon of pMoleButton = the short id of image  "mole_laugh2" then
               set the icon of pMoleButton to "mole_laugh3"
            else if the icon of pMoleButton = the short id of image "mole_laugh3" then
               set the icon of pMoleButton to "mole_laugh2"
            end if
         end if
      end if
      
      if sTopCounter[pMoleButton] >30 then
         put 0 into sTopCounter[pMoleButton]
         put "down" into sMoveDirection[pMoleButton]
         set the top of pMoleButton to the top of pMoleButton+10
      else
         add 1 to sTopCounter[pMoleButton]
      end if
   else
      -- mole is moving up or down
      if sMoveDirection[pMoleButton] = "up" then
         set the top of pMoleButton to the top of pMoleButton-20
      else
         set the top of pMoleButton to the top of pMoleButton+20
      end if
   end if
end moveMole

on stopGame
   cancel sMessage
   repeat with X=1 to 3
      set the top of btn ("mole"&X) to the top of image "grass lower" + 60
   end repeat
end stopGame

on smashMole
   put true into sSmashedMole[the long name of the target]
end smashMole
