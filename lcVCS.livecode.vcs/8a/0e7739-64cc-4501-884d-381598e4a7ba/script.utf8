-- This script defines the behavior of your data grid's custom template. This behavior
-- only applies to 'forms' and not 'tables'.

on FillInData pDataArray
   set the text of field "title" of me to pDataArray["repository"]["name"]
   set the text of field "description" of me to pDataArray["repository"]["description"]
   set the text of field "owner" of me to pDataArray["repository"]["owner"]["login"]
   set the filename of image "owner" of me to pDataArray["repository"]["owner"]["avatar_url"]
end FillInData


on LayoutControl pControlRect
   local theFieldRect
   set the width of fld "owner" of me to the formattedWidth of fld "owner" of me
   set the right of fld "owner" of me to item 3 of pControlRect
   
   put the rect of field "title" of me into theFieldRect
   put the left of fld "owner" of me into item 3 of theFieldRect
   set the rect of field "title" of me to theFieldRect
   
   set the right of image "owner" of me to item 3 of pControlRect -5
   
   put the rect of field "description" of me into theFieldRect
   put the left of image "owner" of me into item 3 of theFieldRect
   set the rect of field "description" of me to theFieldRect
   put item 2 of theFieldRect + the formattedHeight of field "description" of me into item 4 of theFieldRect
   set the rect of field "description" of me to theFieldRect
   
   put max(item 4 of theFieldRect,the bottom of image "owner" of me+5) into item 4 of pControlRect
   set the rect of graphic "Background" of me to pControlRect
end LayoutControl


on ResetData
    -- Sent when data is being emptied because the control is no longer being used to display data
    set the text of field "title" of me to empty
end ResetData


on PreFillInData
    -- Sent right before new data is going to replace existing data in the control
end PreFillInData


setprop dgHilite pBoolean
    -- This custom property is set when the highlight of your custom template has
    -- changed. By default the "Background" graphic will be highlighted for you. 
    -- You only add script here if you want to further customize the highlight.
    
    -- Example:
    if pBoolean then
        set the foregroundColor of me to the dgProp["hilited text color"] of the dgControl of me
    else
        set the foregroundColor of me to empty
    end if
end dgHilite


getprop dgDataControl
    -- Required by library so that it can locate your control.
    return the long ID of me
end dgDataControl


